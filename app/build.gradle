plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-android-extensions'
}

def app_properties = new Properties()
app_properties.load(new FileInputStream(rootProject.file("app.properties")))

def gitCommitNumber = {
    try {
        return 'git rev-list --count --first-parent HEAD'.execute().text.trim().toInteger().intValue()
    } catch (Exception ignored) {
        return 0
    }
}

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "me.hectorhalpizar.android.nytimes"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode gitCommitNumber()
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "NY_TIMES_REST_API_KEY", "\"${app_properties["ny_times.api_key"]}\"")
        buildConfigField("int", "APP_VERSION_CODE", "$versionCode")
        buildConfigField("String", "APP_VERSION_NAME", "\"$versionName\"")
    }

    signingConfigs {
        release {
            keyAlias = app_properties["keyAlias"]
            keyPassword = app_properties["keyPassword"]
            storeFile = file(app_properties["storeFile"])
            storePassword = app_properties["storePassword"]
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    testOptions {
        unitTests {
            includeAndroidResources  = true
            returnDefaultValues = true
        }
        animationsDisabled = true
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    api "com.google.android.material:material:$versions.material"
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.2'
    implementation 'androidx.viewpager:viewpager:1.0.0'
    kapt "androidx.room:room-compiler:$versions.room"
    kapt "org.jetbrains.kotlinx:kotlinx-metadata-jvm:0.3.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.lifecycle:lifecycle-extensions:$versions.lifecycle_extensions"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycle_vm_ktx"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$versions.lifecycle_vm_saveStatee"
    implementation "androidx.room:room-runtime:$versions.room"
    implementation "androidx.room:room-ktx:$versions.room"
    implementation "com.google.android.material:material:$versions.material"
    implementation "com.google.code.gson:gson:$versions.gson"
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit2"
    implementation "com.squareup.retrofit2:converter-gson:$versions.retrofit2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$versions.kotlin_coroutines"
    implementation "com.github.bumptech.glide:glide:$versions.glide"
    implementation 'androidx.core:core-splashscreen:1.0.0-beta02'

    implementation(project(":core"))

    annotationProcessor "androidx.lifecycle:lifecycle-compiler:$versions.lifecycle"
    annotationProcessor "com.github.bumptech.glide:compiler:$versions.glide"

    testImplementation 'junit:junit:4.13.2'
    testImplementation "io.mockk:mockk:$versions.mockk"
    testImplementation "io.mockk:mockk-agent-jvm:$versions.mockk"
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.1'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    // AndroidX Test - Instrumented testing
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.4.0'
}